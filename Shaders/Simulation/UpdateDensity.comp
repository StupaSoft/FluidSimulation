#version 450

const uint OVERLAPPING_BUCKETS = 8;
const float PI = 3.141592;

struct SimulationSetup
{
    uint particleCount;
};

struct GridSetup
{
    uvec4 dimension;
};

struct SimulationParameters
{
	float particleRadius;
	float particleMass;

	float timeStep;

	vec4 gravitiy;

	float targetDensity;
	float soundSpeed;
	float eosExponent;
	float kernelRadiusFactor;

	float dragCoefficient;
	float viscosityCoefficient;
	float restitutionCoefficient;
	float frictionCoefficient;
};

// ========== Inputs ==========
layout (std430, binding = 0) buffer SimulationSetupInSSBO
{
    SimulationSetup simulationSetup;
};

layout (std430, binding = 1) buffer gridSetupInSSBO
{
    GridSetup gridSetup;
};

layout (std430, binding = 2) buffer PositionInSSBO
{
    vec3 positions[];
};

layout (std430, binding = 3) buffer SimulationParametersInSSBO
{
    SimulationParameters simulationParameters;
};

layout (std430, binding = 4) buffer HashResultInSSBO
{
	uint hashResults[]; // [# of particles]
};

layout (std430, binding = 5) buffer AccumulationInSSBO
{
	// After this stage, the accumulations will contain starting indices of each bucket 
    uint accumulations[]; // [# of buckets]
};

layout (std430, binding = 6) buffer BucketOutSSBO
{
	uint buckets[]; // [# of particles]
};

layout (std430, binding = 7) buffer AdjacentBucketInSSBO 
{
	uint adjacentBuckets[]; // [# of particles * 8]
};

// ========== Outputs ==========
layout (std430, binding = 8) buffer DensityOutSSBO
{
    float densities[];
};

// ========== Execution ==========
layout (local_size_x = 1024, local_size_y = 1, local_size_z = 1) in;

float Kernel(float dist, float r1, float r2, float r3)
{
	float x = 1.0f - dist * dist / r2;
	return (315.0f * x * x * x) / (64.0f * PI * r3);
}

void main() 
{
	uint particleIndex = gl_GlobalInvocationID.x;
	if (particleIndex >= simulationSetup.particleCount) return;

	float kernelRange1 = simulationParameters.particleRadius * simulationParameters.kernelRadiusFactor;
	float kernelRange2 = kernelRange1 * kernelRange1;
	float kernelRange3 = kernelRange2 * kernelRange1;
	vec3 particlePosition = positions[particleIndex];

	float sum = 0.0f;
	for (uint i = 0; i < OVERLAPPING_BUCKETS; ++i)
	{
		uint hashKey = adjacentBuckets[particleIndex * OVERLAPPING_BUCKETS + i];

		uint neighborStart = hashKey == 0 ? 0 : accumulations[hashKey - 1];
		uint neighborEnd = accumulations[hashKey]; // Exclusive end

		for (uint j = neighborStart; j < neighborEnd; ++j)
		{
			// Take itself into account
			uint neighborIndex = buckets[j];
			float distanceToNeighbor = distance(particlePosition, positions[neighborIndex]);
			if (distanceToNeighbor < kernelRange1)
			{
				sum += Kernel(distanceToNeighbor, kernelRange1, kernelRange2, kernelRange3);
			}
		}
	}

	densities[particleIndex] = sum * simulationParameters.particleMass;	
}