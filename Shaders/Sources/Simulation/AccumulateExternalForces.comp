#version 450

struct SimulationSetup
{
    uint particleCount;
};

struct GridSetup
{
    uvec4 dimension;
};

struct SimulationParameters
{
	float particleRadius;
	float particleMass;

	float timeStep;

	vec4 gravitiy;

	float targetDensity;
	float soundSpeed;
	float eosExponent;
	float kernelRadiusFactor;

	float dragCoefficient;
	float viscosityCoefficient;
	float restitutionCoefficient;
	float frictionCoefficient;
};

// ========== Inputs ==========
layout (std430, binding = 0) buffer SimulationSetupInSSBO
{
    SimulationSetup simulationSetup;
};

layout (std430, binding = 1) buffer GridSetupInSSBO
{
    GridSetup gridSetup;
};

layout (std430, binding = 2) buffer PositionInSSBO
{
    vec3 positions[];
};

layout (std430, binding = 3) buffer SimulationParametersInSSBO
{
    SimulationParameters simulationParameters;
};

layout (std430, binding = 4) buffer VelocityInSSBO
{
    vec3 velocities[];
};

// ========== Outputs ==========
layout (std430, binding = 5) buffer ForceOutSSBO
{
    vec3 forces[];
};

// ========== Execution ==========
layout (local_size_x = 1024, local_size_y = 1, local_size_z = 1) in;

vec3 GetWindVelocityAt(vec3 pos)
{
	return vec3(0.0f, 0.0f, 0.0f);
}

void main() 
{
	uint particleIndex = gl_GlobalInvocationID.x;
	if (particleIndex >= simulationSetup.particleCount) return;

	// Apply gravity
	vec3 externalForce = simulationParameters.particleMass * simulationParameters.gravitiy.xyz;

	// Apply wind forces
	vec3 relativeVelocity = velocities[particleIndex] - GetWindVelocityAt(positions[particleIndex]);
	externalForce += -simulationParameters.dragCoefficient * relativeVelocity;

	forces[particleIndex] += externalForce;
}